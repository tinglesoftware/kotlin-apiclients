// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.20'
    ext.min_sdk_version = 21
    ext.compile_sdk_version = 30
    ext.target_sdk_version = 30

    ext.group_id = 'tingle.software.api'
    ext.vsts_url = 'https://pkgs.dev.azure.com/tingle/_packaging/tingle-maven/maven/v1'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath 'digital.wup:android-maven-publish:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "net.linguica.maven-settings" version "0.5"
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

ext.coalesce = { param1, param2 -> return param1 != null ? param1 : param2 }
ext.getPublishVersion = { -> return coalesce(System.getenv("BUILD_BUILDNUMBER"), "1.0") }
ext.getPublishUserName = { ->
    return coalesce(System.getenv("PACKAGING_USERNAME"), "AZURE_ARTIFACTS")
}
ext.getPublishPassword = { ->
    return coalesce(System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN"), "dummy")
}
ext.getPublishUrl = { -> return coalesce(System.getenv("PACKAGING_URL"), "${vsts_url}") }

task clean(type: Delete) {
    delete rootProject.buildDir
}
