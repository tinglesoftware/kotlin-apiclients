plugins {
    id "com.android.library"
    id "kotlin-android"
    id "maven-publish"
    id "signing"
}

android {
    compileSdkVersion Integer.parseInt("${compile_sdk_version}")

    defaultConfig {
        minSdkVersion Integer.parseInt("${min_sdk_version}")
        targetSdkVersion Integer.parseInt("${target_sdk_version}")
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

afterEvaluate {
    // See https://developer.android.com/studio/build/maven-publish-plugin
    // and https://docs.gradle.org/current/userguide/publishing_maven.html
    // and https://proandroiddev.com/android-maven-publish-for-your-libraries-b76ad47677df
    publishing {
        // Creates a Maven publication called "release".
        publications {
            release(MavenPublication) {
                groupId = "${groupId}"
                artifactId = "${project.name}"
                version = publishVersion

                pom {
                    name = project.name
                    packaging = "aar"
                    description = "An abstract API client library build on top of Okhttp library to ease the process of making various server request"
                    url = "https://github.com/tinglesoftware/kotlin-apiclients"

                    scm {
                        url = "https://github.com/tinglesoftware/kotlin-apiclients"
                        connection = "scm:git:github.com/tinglesoftware/kotlin-apiclients"
                        developerConnection = "scm:git:ssh://github.com/tinglesoftware/kotlin-apiclients.git"
                    }

                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://raw.githubusercontent.com/tinglesoftware/kotlin-apiclients/main/LICENSE"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "tingle-software"
                            name = "Tingle Software"
                            email = "info@tingle.software"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url publishUrl
                credentials {
                    username publishUsername
                    password publishPassword
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api "androidx.legacy:legacy-preference-v14:1.0.0"
    api 'androidx.annotation:annotation:1.2.0'
    api 'com.google.code.gson:gson:2.8.7'
    api 'com.squareup.okhttp3:okhttp:4.9.1'
    api 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    api "com.google.firebase:firebase-auth:21.0.1"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.0-native-mt"


    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
